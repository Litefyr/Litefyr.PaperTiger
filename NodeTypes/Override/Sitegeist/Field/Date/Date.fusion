prototype(Sitegeist.PaperTiger:Field.Date) < prototype(Neos.Neos:ContentComponent) {
    placeholder.@process.required = ${this.isRequired && !this.label ? value + ' *' : value}

    useRelativeMinMax = ${q(node).property('useRelativeMinMax')}
    earliestDate.@if.set = ${!this.useRelativeMinMax}
    latestDate.@if.set = ${!this.useRelativeMinMax}

    relativeEarliestDate = ${q(node).property('relativeEarliestDate')}
    relativeEarliestDate.@if.set = ${this.useRelativeMinMax}
    relativeLatestDate = ${q(node).property('relativeLatestDate')}
    relativeLatestDate.@if.set = ${this.useRelativeMinMax}

    // Dirty hack to remove the time from validation error messages
    prototype(Neos.Fusion.Form:ErrorRenderer) {
        renderer.@process.removeTime = ${value ? String.replace(value, ', 00:00:00', '') : value}
    }

    @private {
        relativeDate = Neos.Fusion:DataStructure {
            earliest = ${props.relativeEarliestDate}
            latest = ${props.relativeLatestDate}
        }
        fileLoader = Carbon.FileLoader:Strategy.Attributes {
            @if.hasRelativeDate = ${props.relativeEarliestDate || props.relativeLatestDate}
            package = 'Litefyr.PaperTiger'
            eventOnLoad = 'papertiger-relativedate:init'
            js = 'RelativeDate.js'
        }
    }

    renderer {
        placeholder = ${props.placeholder || null}
        content.attributes {
            @apply.file = ${private.fileLoader}
            x-data = ${private.relativeDate ? AlpineJS.function('relativeDate', private.relativeDate) : null}
            :min = ${props.relativeEarliestDate ? 'min' : null}
            :max = ${props.relativeLatestDate ? 'max' : null}
        }
    }
}
